dnl Process this file with autoconf to produce a configure script.
AC_INIT(src/cd.c)

LIBCDLYTE_MAJOR_VERSION=0
LIBCDLYTE_MINOR_VERSION=9
LIBCDLYTE_PATCH_LEVEL=6
LIBCDLYTE_VERSION=$LIBCDLYTE_MAJOR_VERSION.$LIBCDLYTE_MINOR_VERSION.$LIBCDLYTE_PATCH_LEVEL
AC_SUBST(LIBCDLYTE_MAJOR_VERSION)
AC_SUBST(LIBCDLYTE_MINOR_VERSION)
AC_SUBST(LIBCDLYTE_PATCH_LEVEL)
AC_SUBST(LIBCDLYTE_VERSION)

LIBCDLYTE_INTERFACE_VERSION=0
LIBCDLYTE_BINARY_VERSION=0
AC_SUBST(LIBCDLYTE_INTERFACE_VERSION)
AC_SUBST(LIBCDLYTE_BINARY_VERSION)

dnl Calculate libtool versioning information
LT_RELEASE=$LIBCDLYTE_MAJOR_VERSION.$LIBCDLYTE_MINOR_VERSION
LT_CURRENT=$LIBCDLYTE_INTERFACE_VERSION
LT_REVISION=`expr $LIBCDLYTE_PATCH_LEVEL - $LIBCDLYTE_INTERFACE_VERSION`
LT_AGE=`expr $LIBCDLYTE_INTERFACE_VERSION - $LIBCDLYTE_BINARY_VERSION`
AC_SUBST(LT_RELEASE)
AC_SUBST(LT_CURRENT)
AC_SUBST(LT_REVISION)
AC_SUBST(LT_AGE)

PACKAGE=libcdlyte

AM_INIT_AUTOMAKE($PACKAGE, ${LIBCDLYTE_VERSION})
AM_CONFIG_HEADER(config.h)

AC_ARG_PROGRAM

AC_PROG_MAKE_SET
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AC_CANONICAL_HOST
AM_PROG_CC_STDC
AM_PROG_LIBTOOL

threads=no

AC_ARG_ENABLE(threads,
[  --enable-threads        build a thread-safe version of libcdlyte [default=guessed]],
[if test "$enableval" = "yes"
then
        threads=yes
else
        threads=no
fi])

dnl POSIX.4 threads   
if test "$threads" = "yes"
then
        AC_CHECK_LIB(pthread,pthread_create,threads=-lpthread,threads=no)
        if test "$threads" = "no"
        then
                AC_CHECK_LIB(c_r,pthread_create,threads=-lc_r,threads=no)
        fi
fi

case "$host_os" in
  irix*) 
    AC_DEFINE(IRIX_CDLYTE)
    LIBRARY_LIBS="-lcdaudio -lmediad -lds $LIBS"
    ;;
  *solaris*)
    AC_DEFINE(SOLARIS_GETMNTENT)
    AC_DEFINE(BROKEN_SOLARIS_LEADOUT)
    ;;
esac

AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h sys/ioctl.h unistd.h linux/cdrom.h linux/ucdrom.h sys/cdio.h io/cam/cdrom.h stdarg.h mntent.h sys/mntent.h sys/ucred.h sys/mount.h)

AC_C_CONST

case "$cross_compiling" in
  yes) 
    echo "You are cross compiling.  You will need to edit config.h and specify the target machine's endianness and word size manually."
    ;;
  no)
    AC_C_BIGENDIAN
    AC_CHECK_SIZEOF(long)
    ;;
esac

AC_HEADER_TIME

AC_PROG_GCC_TRADITIONAL
AC_CHECK_FUNCS(gethostbyname gethostbyname_r mkdir socket strerror strstr strtol snprintf getmntinfo)
AC_FUNC_GETMNTENT

if test "$threads" != "no"
then
        AC_DEFINE(HAVE_PTHREAD)
        CFLAGS="$CFLAGS -D_REENTRANT"
        LIBRARY_LIB="$threads $LIBRARY_LIB"
        REENTRANT="-D_REENTRANT"
fi
AC_SUBST(REENTRANT)

AC_SUBST(LIBS)
AC_SUBST(LIB_LDADD)

AC_OUTPUT([
Makefile 
src/Makefile
src/cdlyte.h
tests/Makefile
])
